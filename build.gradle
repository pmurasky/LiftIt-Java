plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'info.solidsoft.pitest' version '1.19.0-rc.3'
    id 'org.openrewrite.rewrite' version '7.26.0'
}

group = 'com.liftit'
version = '0.1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java.srcDir 'src/integrationTest/java'
        resources.srcDir 'src/integrationTest/resources'
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-liquibase'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'org.springframework.security:spring-security-crypto'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation platform('org.testcontainers:testcontainers-bom:1.21.3')
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    rewrite platform('org.openrewrite.recipe:rewrite-recipe-bom:latest.release')
    rewrite 'org.openrewrite.recipe:rewrite-spring'
    rewrite 'org.openrewrite.recipe:rewrite-java-dependencies'
    rewrite 'org.openrewrite.recipe:rewrite-static-analysis'
}

rewrite {
    // Tier 1 â€” always active. See docs/adr/0002-use-openrewrite-for-automated-refactoring.md
    activeRecipe(
        'org.openrewrite.staticanalysis.CommonStaticAnalysis',
        'org.openrewrite.staticanalysis.CodeCleanup',
        'org.openrewrite.staticanalysis.MissingOverrideAnnotation',
        'org.openrewrite.staticanalysis.InstanceOfPatternMatch',
        'org.openrewrite.staticanalysis.FinalizePrivateFields',
        'org.openrewrite.staticanalysis.JavaApiBestPractices',
    )
}

test {
    useJUnitPlatform()
    description = 'Runs unit tests (fast, isolated, no Spring context).'
}

processIntegrationTestResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests (full Spring context, slower).'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    shouldRunAfter test
}

check.dependsOn integrationTest

pitest {
    pitestVersion = '1.22.0'
    junit5PluginVersion = '1.2.3'
    targetClasses = ['com.liftit.*']
    targetTests = ['com.liftit.*']
    excludedTestClasses = ['com.liftit.*IntegrationTest', 'com.liftit.*IT']
    threads = 4
    outputFormats = ['HTML']
    mutationThreshold = 80
    testSourceSets = [sourceSets.test]
    reportDir = layout.buildDirectory.dir('reports/pitest')
}
